programa
{
	inclua biblioteca Sons
	inclua biblioteca Texto --> t
	inclua biblioteca Util --> u
	
	cadeia palavra , letra, cheats, nome
	inteiro quant
     inteiro classeStats[6]
     inteiro classe
     cadeia equip[2]
     cadeia yn
     inteiro vidabase = 1000
     inteiro sorteio

     inteiro vida_inimigo = 0

	const inteiro VELOCIDADE = 20
	const inteiro DIALOGO = 40

	funcao escrevaslow(cadeia texto, inteiro velocidade)
	{
	 	para(inteiro i=0; i < t.numero_caracteres(texto); i++)
	 	{
	 		escreva(t.obter_caracter(texto, i))
	 		u.aguarde(velocidade)

		}

	}
     
	funcao inicio()
	{
		escreva("oooooooooo.  ooooo          .oooooo.     .oooooo.   oooooooooo.   oooooooooo.    .oooooo.   ooooooooo.   ooooo      ooo oooooooooooo \n")
		escreva("`888'   `Y8b `888'         d8P'  `Y8b   d8P'  `Y8b  `888'   `Y8b  `888'   `Y8b  d8P'  `Y8b  `888   `Y88. `888b.     `8' `888'     `8 \n")
		escreva(" 888     888  888         888      888 888      888  888      888  888     888 888      888  888   .d88'  8 `88b.    8   888          \n")
		escreva(" 888oooo888'  888         888      888 888      888  888      888  888oooo888' 888      888  888ooo88P'   8   `88b.  8   888oooo8    \n")
		escreva(" 888    `88b  888         888      888 888      888  888      888  888    `88b 888      888  888`88b.     8     `88b.8   888         \n")
		escreva(" 888    .88P  888       o `88b    d88' `88b    d88'  888     d88'  888    .88P `88b    d88'  888  `88b.   8       `888   888       o \n")
		escreva("o888bood8P'  o888ooooood8  `Y8bood8P'   `Y8bood8P'  o888bood8P'   o888bood8P'   `Y8bood8P'  o888o  o888o o8o        `8  o888ooooood8 \n\n")

		 escreva("------------------------------------------------------aperte e para começar--------------------------------------------------------\n")
		  	
		leia(letra)
		
           se(letra == "e") {
           	escrevaslow("Qual seu nome?: ", VELOCIDADE)
           	leia(nome)                                                                                                                      
           }
            
            escrevaslow("...que nome intrigante.", DIALOGO)
            escrevaslow("\nQual sua classe?: ", VELOCIDADE)
            
            escrevaslow("\n1) Carcereiro de Hemwick\n", VELOCIDADE)
		  escrevaslow("2) Discípulo da Luz Lunar\n", VELOCIDADE)
		  escrevaslow("3) Espião de Cainhurst\n", VELOCIDADE)
		  escrevaslow("4) Primeiro caçador\n", VELOCIDADE)
		  

		  

		 faca {
			escrevaslow("informe a classe: ",VELOCIDADE)
			leia(classe)
		 	
			escolha(classe){
				caso 1:
				{
					escreva("Carcereiro de Hemwick: \n")
					escreva(" vit: 17\n vig: 17 \n for: 19\n dex: 14\n bld: 12\n arc: 12\n")
					escrevaslow("Deseja escolher esta classe? Y/N: ", DIALOGO)
					leia(yn)
						yn = t.caixa_baixa(yn)
					se(yn == "y"){
						classeStats[0] = 17
						classeStats[1] = 17
						classeStats[2] = 19
						classeStats[3] = 14
						classeStats[4] = 12
						classeStats[5] = 12
						equip[0] = "cutelo serrado"
						equip[1] = "pistola"
						  	   }
					
					pare
				}
				caso 2:
				{
					escreva("Discípulo da Luz Lunar: \n")
					escreva(" vit: 16\n vig: 15 \n for: 18\n dex: 15\n bld: 12\n arc: 16\n")
					escrevaslow("Deseja escolher esta classe? Y/N: ", DIALOGO)
					leia(yn)
					yn = t.caixa_baixa(yn)

					se(yn == "y"){
						classeStats[0] = 16
						classeStats[1] = 15
						classeStats[2] = 18
						classeStats[3] = 15
						classeStats[4] = 12
						classeStats[5] = 16
						equip[0] = "lamina sagrada de ludwig"
						equip[1] = "pistola"
							   }
					pare
				}
				caso 3:
				{
					escreva("Espião de Cainhurst: \n")
					escreva(" vit: 15\n vig: 15 \n for: 14\n dex: 19\n bld: 15\n arc: 12\n")
					escrevaslow("Deseja escolher esta classe? Y/N: ", DIALOGO)
					leia(yn)
					yn = t.caixa_baixa(yn)

					se(yn == "y"){
						classeStats[0] = 15
						classeStats[1] = 15
						classeStats[2] = 14
						classeStats[3] = 19
						classeStats[4] = 15
						classeStats[5] = 12
						equip[0] = "rakuyo"
						equip[1] = "evelyn"	
							   }
					pare
				}
				caso 4:
				{
					escreva("Primeiro caçador: \n")
						escreva(" vit: 19\n vig: 15 \n for: 17\n dex: 18\n bld: 15\n arc: 12\n")
						escrevaslow("Deseja escolher esta classe? Y/N: ", DIALOGO)
						leia(yn)
						yn = t.caixa_baixa(yn)
	
						se(yn == "y"){
							classeStats[0] = 15
							classeStats[1] = 15
							classeStats[2] = 14
							classeStats[3] = 19
							classeStats[4] = 15
							classeStats[5] = 12
							equip[0] = "lamina funeraria"
							equip[1] = "blunderbuss"
								   }
				 	pare			
				}

			}
			
		 } enquanto(yn != "y")//Fim enquanto

		 escrevaslow("\nIndependente de sua origem, voce foi atraido para yharnam por uma unica razao.",DIALOGO)
		 escrevaslow("\no sangue palido, uma substancia capaz de curar qualquer ferimento.,",DIALOGO)
		 escrevaslow("\nApos uma rapida transfusao de sangue palido, voce acorda em uma clinica, sem memoria de nada\nmas voce não esta sozinho",DIALOGO)
		 batalha(10, 2000)
		 
		 escrevaslow("\nContinuando sua jornada para descobrir oque diabos aconteceu aqui nessa cidade, \nvoce se depara com uma pequena lampada, por pura curiosidade voce decide encostar nela...",DIALOGO)
		 escrevaslow("*FWOOOSH* De repente, a lampada te teleporta para um lugar uma estranho.\nA sua frente á uma escada dando entrada á uma pequena igreja, voce esta rodeado por lápides de diferentes tamanhos e materiais",DIALOGO)
		 escrevaslow("\nO que voce quer fazer?: ",DIALOGO)
	}
			



	funcao batalha(inteiro numero_turnos, inteiro vida_homiruim){
		vida_inimigo = vida_homiruim

		bixospeak()
		enquanto (numero_turnos > 0 e vida_inimigo > 0){
			
			cadeia frase = "\nTURNO: " + numero_turnos
			
			escrevaslow(frase, VELOCIDADE)
			
			kombat()
			numero_turnos --

			se(vida_inimigo <= 0){
				escreva("\n*A fera parou de se mover, Bom trabalho!*")
				pare
			}
		}

		//verificar se venceu ou nao a batalha depois de n turnos
	}
	

	funcao kombat(){
		

		inteiro arma
		inteiro kapow = 30
		inteiro gun = 20

		cadeia frasekapow = "\n*Voce deu " + (kapow * classeStats[2]) + " de dano, continue*"
		cadeia frasegun = "\n*Voce atirou e deu " + (gun * classeStats[4]) + " de dano, continue*"
		//se or arma branca: chance de 1 a 3 de causar dano critico
		//se usar arma de fogo: chance de 50% de estunar e ganhar mais um turno
		//caso apos estunar usar arma branca, dar dobro de dano 

		escreva("\nEscolha sua arma: ")

		para(inteiro i=0; i < 2; i++){
			escreva("\n",i,"° ", equip[i])
		}

		leia(arma)
		
		se(arma == 0){
			sorteio = u.sorteia(1, 3)


			se(sorteio == 1){
				vida_inimigo = vida_inimigo - (kapow * classeStats[2]) * 2
				 escrevaslow("\n*Acerto critico, nao falta muito...*", DIALOGO)
			}senao{
				vida_inimigo = vida_inimigo - (kapow * classeStats[2])
				escrevaslow(frasekapow,DIALOGO)
			}

			
		
		}senao se (arma == 1) {
			sorteio = u.sorteia(1, 2)
			se(sorteio == 2)
			{
				vida_inimigo = vida_inimigo - (gun * classeStats[4]) * 2
				escrevaslow("*BOOM! headshot, agora e a sua chance, ataca...*",DIALOGO)
 			}senao{
 				vida_inimigo = vida_inimigo - (gun * classeStats[4])
 				escreva(frasegun,DIALOGO)
 			}
		}
		
	}
		
		funcao bixospeak()
		{
		
		sorteio = u.sorteia(1, 5)
		 
		 se(sorteio == 1)
		 {
		 	escrevaslow("\nUma fera se aproxima",DIALOGO)
		 }senao se(sorteio == 2){
		 	escrevaslow("\nUm atirador lhe avistou", DIALOGO)
		 }senao se(sorteio == 3){
		 	escrevaslow("\nUm executor te encontrou", DIALOGO)
		 }senao se(sorteio == 4){
		 	escrevaslow("\nIsso antes era humano, agora não é mais", DIALOGO)
		 }senao se(sorteio == 5){
		 	escrevaslow("\nCONTEM DOENÇA, MORTE!", DIALOGO)
		 }

		}
		 			

	
}
